# Forked version of github.com/protobuf/protobuf/cmake protobuf-generate.cmake
# See the protobuf LICENSE for more details:
#
# Copyright 2008 Google Inc.  All rights reserved.
#
# Redistribution and use in source and binary forms, with or without
# modification, are permitted provided that the following conditions are
# met:
#
#    * Redistributions of source code must retain the above copyright
# notice, this list of conditions and the following disclaimer.
#     * Redistributions in binary form must reproduce the above
# copyright notice, this list of conditions and the following disclaimer
# in the documentation and/or other materials provided with the
# distribution.
#     * Neither the name of Google Inc. nor the names of its
# contributors may be used to endorse or promote products derived from
# this software without specific prior written permission.
#

# Invokes the protoc compiler on the .proto sources in a PROTO_TARGET, adding
# the generated outputs to the TARGET along with the computed import directories.
#
# btc_protobuf(
#   TARGET <source>
#     Build .proto files included in the target.
#
#   PROTO_TARGET <proto>
#     Target used to resolve .proto imports.
#
#   PROTOC_OPTIONS  <options list>
#     Addional options passed to protoc
#
#   LANGUAGE  <language>
#     Generated language, such as cpp, upb, etc. Should match the plugin.
#   GENERATE_EXTENSIONS  <extension list>
#     Extensions generated by the protoc compiler for each file.
#     For example, ".pb.h" ".pb.cc" are the cpp extensions.
#   PLUGIN  <plugin executable> 
#     Plugin executable name [empty for cpp]. Typically something like:
#     protoc-gen-${LANGUAGE}=$<TARGET_FILE:lanugage_plugin_target>
#   PLUGIN_OPTIONS
#    Options passed to the plugin.
#   PROTOC_OUT_DIR  <directory>
#     Output directory for generated .c/.h files
#
#   IMPORT_VARS <list>
#     List of variable used to reslove imports. For defined variables,
#     contents will be added to the import flags.
#
#   DEPENDENCIES  <dependencies>
#    CMake dependencies.
# )
function(btc_protobuf)
  include(CMakeParseArguments)

  set(_singleargs TARGET PROTO_TARGET LANGUAGE PROTOC_OUT_DIR PLUGIN PLUGIN_OPTIONS DEPENDENCIES)
  set(_multiargs IMPORT_VARS GENERATE_EXTENSIONS PROTOC_OPTIONS)

  cmake_parse_arguments(btc_protobuf "" "${_singleargs}" "${_multiargs}" "${ARGN}")

  if(NOT btc_protobuf_TARGET)
    message(SEND_ERROR "Error: btc_protobuf called without a TARGET")
    return()
  endif()

  if(NOT btc_protobuf_LANGUAGE)
    set(btc_protobuf_LANGUAGE cpp)
  endif()
  string(TOLOWER ${btc_protobuf_LANGUAGE} btc_protobuf_LANGUAGE)

  if(NOT btc_protobuf_GENERATE_EXTENSIONS)
    message(SEND_ERROR "Error: btc_protobuf called without GENERATE_EXTENSIONS for LANGUAGE ${btc_protobuf_LANGUAGE}.  ${btc_protobuf_TARGET}")
    return()
  endif()

  if(NOT btc_protobuf_PROTO_TARGET)
    message(SEND_ERROR "Error: btc_protobuf called without a PROTO_TARGET. ${btc_protobuf_TARGET}")
    return()
  endif()

  if(NOT btc_protobuf_PROTOC_OUT_DIR)
    set(btc_protobuf_PROTOC_OUT_DIR ${CMAKE_CURRENT_BINARY_DIR})
  endif()

  # Get the sources from the PROTO_TARGET
  get_property(_source_list TARGET ${btc_protobuf_PROTO_TARGET} PROPERTY INTERFACE_SOURCES)
  foreach(_file ${_source_list})
    if(_file MATCHES "proto$")
      list(APPEND btc_protobuf_PROTOS ${_file})
    endif()
  endforeach()

  get_property(_source_list TARGET ${btc_protobuf_PROTO_TARGET} PROPERTY SOURCES)
  foreach(_file ${_source_list})
    if(_file MATCHES "proto$")
      list(APPEND btc_protobuf_PROTOS ${_file})
    endif()
  endforeach()

  if(NOT btc_protobuf_PROTOS)
    message(SEND_ERROR "Error: protobuf_generate could not find any .proto files.  ${btc_protobuf_TARGET}")
    return()
  endif()

  # Construct the plugin options.
  foreach(_option IN LISTS btc_protobuf_PLUGIN_OPTIONS)
    # append comma - not using CMake lists and string replacement as users
    # might have semicolons in options
    if(_plugin_options)
      set( _plugin_options "${_plugin_options},")
    endif()
    set(_plugin_options "${_plugin_options}${_option}")
  endforeach()

  if(btc_protobuf_PLUGIN)
    set(_plugin "--plugin=${btc_protobuf_PLUGIN}")
  endif()

  # Construct transitive includes using generator expressions.
  # Using INTERFACE_LINK_LIBRARIES with generator expressions allows a
  # slightly less strict order of proto_library() target dependencies.
  #
  # The basic expression is:
  # foreach target in INTERFACE_LINK_LIBRARY:
  #   if target exists:
  #     foreach INTERFACE_INCLUDE_DIRECTORY in target:
  #       add -I;path;
  #
  # In addition, the INTERFACE_INCLUDE_DIRECTORIES will also be added without
  # using generator expressions.

  unset(_protobuf_transitive_include)

  get_property(_link_libs TARGET ${btc_protobuf_PROTO_TARGET} PROPERTY INTERFACE_LINK_LIBRARIES)
  foreach(_tgt ${_link_libs})
    # When using system libs, Protobuf targets may be generated which do not exist.
    set(_prop "$<TARGET_PROPERTY:${_tgt},INTERFACE_INCLUDE_DIRECTORIES>")
    set(_expr "-I$<SEMICOLON>$<JOIN:${_prop},$<SEMICOLON>-I$<SEMICOLON>>")
    list(APPEND _protobuf_transitive_include "$<$<TARGET_EXISTS:${_tgt}>:$<$<BOOL:${_prop}>:${_expr}>>")
  endforeach()

  list(REMOVE_DUPLICATES _protobuf_transitive_include)

  # Resolve import directories, which will be used to construct
  # the includes list for the proto compiler.
  unset(_protobuf_imports)
  get_property(_inc TARGET ${btc_protobuf_PROTO_TARGET} PROPERTY INTERFACE_INCLUDE_DIRECTORIES)
  list(APPEND _protobuf_imports ${_inc})

  foreach(_var ${btc_protobuf_IMPORT_VARS})
    if(DEFINED ${_var})
    list(APPEND _protobuf_imports ${_var})
    endif()
  endforeach()

  list(REMOVE_DUPLICATES _protobuf_imports)

  # Construct the include paths
  unset(_protobuf_include_path)
  foreach(_dir ${_protobuf_imports})
    get_filename_component(_abs_path "${_dir}" ABSOLUTE)
    list(FIND _protobuf_include_path "${_abs_path}" _contains_already)
    if(${_contains_already} EQUAL -1)
        list(APPEND _protobuf_include_path "-I" "${_abs_path}")
    endif()
  endforeach()

  list(FIND _protobuf_imports "${btc_protobuf_PROTOC_OUT_DIR}" _contains_already)
  if(${_contains_already} EQUAL -1)
    list(APPEND _protobuf_imports  "${btc_protobuf_PROTOC_OUT_DIR}")
  endif()

  list(REMOVE_ITEM btc_protobuf_DEPENDENCIES protobuf::protoc)

  if(NOT _protobuf_include_path)
    set(_protobuf_include_path -I ${PROJECT_SOURCE_DIR} -I ${PROJECT_BINARY_DIR})
  endif()

  set(_generated_srcs_all)
  foreach(_proto ${btc_protobuf_PROTOS})
    get_filename_component(_abs_file ${_proto} ABSOLUTE)
    get_filename_component(_abs_dir ${_abs_file} DIRECTORY)

    get_filename_component(_file_full_name ${_proto} NAME)
    string(FIND "${_file_full_name}" "." _file_last_ext_pos REVERSE)
    string(SUBSTRING "${_file_full_name}" 0 ${_file_last_ext_pos} _basename)

    set(_suitable_include_found FALSE)
    foreach(DIR ${_protobuf_include_path})
      if(NOT DIR STREQUAL "-I")
        file(RELATIVE_PATH _rel_dir ${DIR} ${_abs_dir})
        string(FIND "${_rel_dir}" "../" _is_in_parent_folder)
        if (NOT ${_is_in_parent_folder} EQUAL 0)
          set(_suitable_include_found TRUE)
          break()
        endif()
      endif()
    endforeach()

    if(NOT _suitable_include_found)
      message(SEND_ERROR "Error: btc_protobuf could not find any correct proto include directory.  ${btc_protobuf_TARGET}")
      return()
    endif()

    set(_generated_srcs)
    foreach(_ext ${btc_protobuf_GENERATE_EXTENSIONS})
      list(APPEND _generated_srcs "${btc_protobuf_PROTOC_OUT_DIR}/${_rel_dir}/${_basename}${_ext}")
    endforeach()
    list(APPEND _generated_srcs_all ${_generated_srcs})

    set(_comment "Running ${btc_protobuf_LANGUAGE} protocol buffer compiler on ${_proto}")
    if(btc_protobuf_PROTOC_OPTIONS)
      set(_comment "${_comment}, protoc-options: ${btc_protobuf_PROTOC_OPTIONS}")
    endif()
    if(_plugin_options)
      set(_comment "${_comment}, plugin-options: ${_plugin_options}")
    endif()

    add_custom_command(
      OUTPUT ${_generated_srcs}
      COMMAND protobuf::protoc
          ${btc_protobuf_PROTOC_OPTIONS} --${btc_protobuf_LANGUAGE}_out
          ${_plugin_options}:${btc_protobuf_PROTOC_OUT_DIR}
          ${_plugin}
          ${_protobuf_include_path}
          "${_protobuf_transitive_include}"
          ${_abs_file}
      DEPENDS ${_abs_file} protobuf::protoc ${btc_protobuf_DEPENDENCIES} ${btc_protobuf_PROTO_TARGET}
      COMMENT ${_comment}
      COMMAND_EXPAND_LISTS
      VERBATIM )
  endforeach()

  set_source_files_properties(${_generated_srcs_all} PROPERTIES GENERATED TRUE)
  target_sources(${btc_protobuf_TARGET} PRIVATE ${_generated_srcs_all})
  set_property(
      TARGET ${btc_protobuf_TARGET}
      APPEND PROPERTY INCLUDE_DIRECTORIES "${btc_protobuf_PROTOC_OUT_DIR}" )
  set_property(
      TARGET ${btc_protobuf_TARGET}
      APPEND PROPERTY INTERFACE_INCLUDE_DIRECTORIES "${btc_protobuf_PROTOC_OUT_DIR}" )

endfunction()


